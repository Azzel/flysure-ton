#!/usr/bin/env fift -s
"common-utils.fif" include

{ ."<workchain-id> [<price>] [<settlement>] [<filename-base>] [-C <code-fif>]" cr
  ."Creates a new flysure contract with <price> amount of policy premium and <settlement> amount for claim payment, with private keys saved to or loaded from <filename-base>.pk" cr
  ."('new-flysure.pk' by default)." cr
  ."some additional info."
} : usage()

// == Parsing command-line arguments

{
  "-C" "--code-file" 1 { +".fif" include =: ContractCode } option-long()

  1 { parse-workchain-id =: WorkchainId } nth-argument()
  2 { $>GR =: price } nth-argument()
  3 { $>GR =: settlement } nth-argument()
  4 { =: FileBase } nth-argument()
  no-more-arguments() // Prevents adding too much arguments
} scan-args()

1 < { // Checks that there's enough arguments
  show-usage()
} if

// Default values
1000000 =:? price // 0.001 GR
10000000 =:? settlement // 0.010 GR
"my-flysure" =:? FileBase
"flysure-code.fif" include =:? ContractCode

// == End of parsing command-line arguments

."Creating new flysure in workchain " WorkchainId . cr

FileBase +".pk" load-generate-keypair
=: PrivateKey
=: PublicKey

<b
  0 32 u, // seqno
  PublicKey B, // owner pubkey
  price Gram,
  settlement Gram,
  0 Gram, // reserve
  null dict, // policies
b> =: ContractData
."ContractData: " ContractData <s csr. cr

ContractCode ContractData null state-init() =: StateInit
."StateInit: " StateInit <s csr. cr

WorkchainId StateInit hashu 2constant ContractAddr
."New contract address = " ContractAddr .addr cr
ContractAddr FileBase +".addr" save-address-verbose

."Non-bounceable address (for init): " ContractAddr 7 .Addr cr
."Bounceable address (for later access): " ContractAddr 6 .Addr cr

0 -1 empty-message() =: MessageBody
."Signing message: " MessageBody <s csr. cr

ContractAddr StateInit
  MessageBody PrivateKey sign-message()
external-message() =: ExtMessage

."External message for initialization is " ExtMessage <s csr. cr
ExtMessage 2 boc+>B dup Bx. cr
FileBase +"-init-query.boc" tuck B>file
."(Saved contract creating query to file " type .")" cr
